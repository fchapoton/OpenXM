/* $OpenXM: OpenXM/src/asir-contrib/packages/src/taka_hg32.rr,v 1.2 2001/09/02 09:17:41 takayama Exp $ */
/*  experimental module for hypergeometric function identities. 
    cf.  nach/3f2hom.rr
*/

def taka_hg21(A1,A2,B2,Z) {
  F = quote(hypergeometric_2F1(A1,A2,B2,Z));
  F2 = eval_variables_in_quote(F);
  return F2;
}

/* z^(-B2+1) * cycle * ds/ds */
def taka_hg21_cycle(A1,A2,B2,Z,S) {
  U=S^(A2)*(Z-S)^(B2-1-A2)*(1-S)^(-A1);
  S1 = quote(topology_open_segment(Z,1));
  S1 = eval_variables_in_quote(S1);
  C1 = quote(algebra_tensor(S1,U));
  C1 = eval_variables_in_quote(C1);

  S2 = quote(topology_open_segment(0,Z));
  S2 = eval_variables_in_quote(S2);
  C2 = quote(algebra_tensor(S2,U));
  C2 = eval_variables_in_quote(C2);

  return([C1,C2]);
}

def taka_hg21_reg_cycle(A1,A2,B2,Z,S) {
  U=S^(A2)*(Z-S)^(B2-1-A2)*(1-S)^(-A1);
  U = quote_new(U);
  S1 = quote(topology_open_segment(Z+epsilon,1-epsilon));
  S1 = eval_variables_in_quote(S1);
  C1 = quote(algebra_tensor(S1,U));
  C1 = eval_variables_in_quote(C1);

  S1a = quote(topology_circle(Z,epsilon));
  S1a = eval_variables_in_quote(S1a);
  C1a = quote(1/(exp(2*c_pi*c_i*(B2-1-A2))-1));
  C1a = eval_variables_in_quote(C1a);

  Sa = quote(algebra_tensor(S1a,C1a*U));
  Sa = eval_variables_in_quote(Sa);

  S1b = quote(topology_circle(1,epsilon));
  S1b = eval_variables_in_quote(S1b);
  C1b = quote(1/(exp(2*c_pi*c_i*(-A1))-1));
  C1b = eval_variables_in_quote(C1b);
  Sb = quote(algebra_tensor(S1b,C1b*U));

  C1 = Sa+Sb+C1;

  /* C2 has not yet been set. */
  return([C1,C2]);
}

def taka_hg21_exp(A1,A2,B2) {
  C = [ c2-exp(2*c_pi*c_i*(-A1)),
        c4-exp(2*c_pi*c_i*A2),
        c5-exp(2*c_pi*c_i*(B2-1-A2))];
  return C;  
}

def taka_hg21_monodromy(A1,A2,B2) {
  M0 = newmat(2,2,
        [[1,0],
         [c5*(1-c4),c4*c5]]);
  M1 = newmat(2,2,
        [[c2*c5,1-c2],
         [0,1]]);
  C = taka_hg21_exp(A1,A2,B2);
  return( [M0,M1,C] );
}


def taka_hg21_intersection_matrix_for_cycles(A1,A2,B2) {
  Ih = newmat(2,2,
      [[quote(1/(c5^(-1)-1) + 1 + 1/(c2^(-1)-1)), quote(-1/(c5^(-1)-1))],
       [quote(-c5^(-1)/(c5^(-1)-1)), quote(1/(c4^(-1)-1) + 1 + 1/(c5^(-1)-1))]]);
  return [Ih,taka_hg21_exp(A1,A2,B2)];
}

end$
