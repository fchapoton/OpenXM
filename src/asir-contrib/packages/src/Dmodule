/* $OpenXM: OpenXM/src/asir-contrib/packages/src/Dmodule,v 1.5 2000/01/21 11:45:43 okutani Exp $ */

/*----------------------*/
/* package for D-module */
/*----------------------*/

#define _DEBUG_Dmodule_ 0

Dmodule_sm1_PID = -1$
Dmodule_Euler2D = [[1],[0,1],[0,1,1]]$

def dmodule_sm1_start() {
  extern Dmodule_sm1_PID;
  if (Dmodule_sm1_PID < 0) {
#if _DEBUG_Dmodule_
    print("starting ox_sm1_forAsir ...");
#endif
    Dmodule_sm1_PID = sm1_start();
#if _DEBUG_Dmodule_
    print("sm1 process number: ", 0); print(Dmodule_sm1_PID);
#endif
  }
  return Dmodule_sm1_PID;
}

def dmodule_sm1_restart() {
  extern Dmodule_sm1_PID;
  Dmodule_sm1_PID = sm1_start();
  return Dmodule_sm1_PID;
}

/* [x1,x2,...] -> [dx1,dx2,...] */
def dmodule_make_dv(V) {
  if (type(V) != 4) {
    error("parameter type error.");
  }
  Dv = [];
  for (I = 0; I < length(V); I++) {
    Dv = append(Dv, [strtov("d" + rtostr(V[I]))]);
  }
  return Dv;
}

/* [x1,x2,...] -> [tx1,tx2,...] */
def dmodule_make_tv(V) {
  if (type(V) != 4) {
    error("parameter type error.");
  }
  Tv = [];
  for (I = 0; I < length(V); I++) {
    Tv = append(Tv, [strtov("t" + rtostr(V[I]))]);
  }
  return Tv;
}

/* [x1,x2,...] -> "x1,x2,..." */
def dmodule_var_tosm1(V) {
  if (type(V) != 4) {
    error("parameter type error.");
  }
  if (V == []) {
    return "";
  }
  V_str = rtostr(V[0]);
  for (I = 1; I < length(V); I++) {
    V_str += "," + rtostr(V[I]);
  }
#if _DEBUG_Dmodule_
  print("variables (sm1 format): ", 0); print(V_str);
#endif
  return V_str;
}

/* asir polynomial -> sm1 string */
/* note! sm1 understand only polynomial with integer coefficients. */
def dmodule_p_tosm1(Poly, V) {
  if (type(Poly) > 2 || type(V) != 4) {
    error("parameter type error.");
  }
  if (Poly == 0) {
    return "0";
  }
  Dpoly = dp_ptod(Poly, V);
  Str_poly = "";
  do {
    Hc = dp_hc(Dpoly);
    Ht = dp_ht(Dpoly);
    HtV = dp_etov(Ht);
#if _DEBUG_Dmodule_
    print("Dpoly: ", 0); print(Dpoly);
    print("Hc: ", 0); print(Hc);
    print("Ht: ", 0); print(Ht);
    print("HtV: ", 0); print(HtV);
    print("size(HtV): ", 0); print(size(HtV));
#endif
    /* coefficient */
    if (type(Hc) < 2) {
      Str_poly += " + (" + rtostr(Hc) + ")";
    }
    else {
#if _DEBUG_Dmodule_
      print("Hc is polynomial.");
#endif
      Str_poly += " + (" + dmodule_p_tosm1(Hc, vars(Hc)) + ")";
    }
#if _DEBUG_Dmodule_
    print("coefficient OK.");
#endif
    /* power product */
    Str_mono = "";
    for (J = 0; J < size(HtV)[0]; J++) {
      if (HtV[J] != 0) {
	Str_mono += " " + rtostr(V[J]^HtV[J]);
      }
    }
    Str_poly += Str_mono;
#if _DEBUG_Dmodule_
    print("power product OK.");
#endif
    Dpoly -= dp_hm(Dpoly);
  } while (Dpoly != 0);
  return Str_poly;
}

/* differential operators with polynomial coefficients -> sm1 string */
/* L_list: [ [[fa(V),[a1,...,an]],...], ... ], coefficient must be polynomial. */
def dmodule_d_op_tosm1(L_list, V) {
  L_list = diff_op_rat2zp(L_list);
  L_str_list = [];
  for (I = 0; I < length(L_list); I++) {
    L_str = "";
    for (J = 0; J < length(L_list[I]); J++) {
      Poly = red(L_list[I][J][0]);
      if (Poly != 0) {
	Str_Poly = "(" + dmodule_p_tosm1(Poly, V) + ")";
	Str_D = "";
	Index = L_list[I][J][1];
	for (K = 0; K < length(Index); K++) {
	  if (Index[K] != 0) {
	    Str_D += " d" + rtostr(V[K]^Index[K]);
	  }
	}
	L_str += " + " + Str_Poly + Str_D;
      }
    }
#if _DEBUG_Dmodule_
    print("L_str: ", 0); print(L_str);
#endif
    if (L_str == "") {
      L_str_list = append(L_str_list, ["0"]);
    }
    else {
      L_str_list = append(L_str_list, [L_str]);
    }
  }
  return L_str_list;
}

def dmodule_d_op_toasir(L_list, V) {
  Dv = dmodule_make_dv(V);
  L_list_asir = [];
  for (I = 0; I < length(L_list); I++) {
    L = L_list[I];
    L_asir = 0;
    for (J = 0; J < length(L); J++) {
      L_asir += L[J][0]*dp_dtop(dp_vtoe(matrix_ltov(L[J][1])), Dv);
    }
    L_list_asir = append(L_list_asir, [L_asir]);
  }
  return L_list_asir;
}

def dmodule_d_op_fromasir(D_list, V) {
  Dv = dmodule_make_dv(V);
  L_list = [];
  for (I = 0; I < length(D_list); I++) {
    D = dp_ptod(D_list[I], Dv);
    L = [];
    if (D == 0) {
      L = append(L, [[0,[0,0]]]);
    }
    else {
      while (D != 0) {
	L = append(L, [[dp_hc(D),vtol(dp_etov(dp_ht(D)))]]);
	D -= dp_hm(D);
      }
    }
    L_list = append(L_list, [L]);
  }
  return L_list;
}

def dmodule_filt(V, W) {
  if (type(V) != 4 || type(W) != 4) {
    error("parameter type error.");
  }
  if (2*length(V) != length(W)) {
    error("a length of list is no match.");
  }
  V = append(V, dmodule_make_dv(V));
  Filt = [];
  for (I = 0; I < length(V); I++) {
    Filt = append(Filt, [V[I], W[I]]);
  }
  return Filt;
}

/* weight (0,...,0,1,...,1) */
def dmodule_order_filt(V) {
  OrderX = []; OrderD = [];
  for (I = 0; I < length(V); I++) {
    OrderX = append(OrderX, [V[I], 0]);
    OrderD = append(OrderD, [strtov("d" + rtostr(V[I])), 1]);
  }
  Order = append(OrderX, OrderD);
#if _DEBUG_Dmodule_
  print("order filtration: ", 0); print(Order);
#endif
  return Order;
}

/* weight (1,...,1,1,...,1) */
def dmodule_bernstein_filt(V) {
  OrderX = []; OrderD = [];
  for (I = 0; I < length(V); I++) {
    OrderX = append(OrderX, [V[I], 1]);
    OrderD = append(OrderD, [strtov("d" + rtostr(V[I])), 1]);
  }
  Order = append(OrderX, OrderD);
#if _DEBUG_Dmodule_
  print("bernstein filtration: ", 0); print(Order);
#endif
}

/* D_ideal: ["...", "...",...], string is sm1 format. */
/* V: [x1,x2,...] */
def dmodule_ch_ideal(D_ideal, V) {
  return dmodule_ch_ideal_generic(D_ideal, [], V);
}

def dmodule_ch_ideal_generic(D_ideal, Param, V) {
  Gb = sm1_gb([ D_ideal, dmodule_var_tosm1(append(V,Param)),
                [dmodule_order_filt(append(V,Param))] ]);
#if _DEBUG_Dmodule_
  print("sm1_gb: ", 0); print(Gb);
#endif
  return Gb[1];
}

def dmodule_ch_ideal_appell1(A, B1, B2, C, V) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell1(A, B1, B2, C, V), V);
  return dmodule_ch_ideal(D_ideal_str, V);
}

def dmodule_ch_ideal_appell2(A, B1, B2, C1, C2, V) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell2(A, B1, B2, C1, C2, V), V);
  return dmodule_ch_ideal(D_ideal_str, V);
}

def dmodule_ch_ideal_appell3(A1, A2, B1, B2, C, V) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell3(A1, A2, B1, B2, C, V), V);
  return dmodule_ch_ideal(D_ideal_str, V);
}

def dmodule_ch_ideal_appell4(A, B, C1, C2, V) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell4(A, B, C1, C2, V), V);
  return dmodule_ch_ideal(D_ideal_str, V);
}

def dmodule_ch_ideal_generic_appell4(A, B, C1, C2, Param, V) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell4(A, B, C1, C2, V), V);
  return dmodule_ch_ideal_generic(D_ideal_str, Param, V);
}

def dmodule_ch_ideal_selberg2(A, B, C, S, V) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_selberg2(A, B, C, S, V), V);
  return dmodule_ch_ideal(D_ideal_str, V);
}

def dmodule_ch_ideal_generic_selberg2(A, B, C, S, Param, V) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_selberg2(A, B, C, S, V), V);
  return dmodule_ch_ideal_generic(D_ideal_str, Param, V);
}

def dmodule_ch_ideal_selberg2_1(A, B, K1, K2, V) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_selberg2_1(A, B, K1, K2, V), V);
  return dmodule_ch_ideal(D_ideal_str, V);
}

def dmodule_ch_ideal_gkz(A, B, V) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_gkz(A, B, V), V);
  return dmodule_ch_ideal(D_ideal_str, V);
}

/* restriction to {t1=0,t2=0,...} */
/* D_ideal: ["...", "...",...], string is sm1 format. */
/* Rest: [t1,t2,...] */
/* V: [x1,x2,...] */
def dmodule_restriction(D_ideal, V, Rest) {
  D_ideal_str = "";
  for (I = 0; I < length(D_ideal); I++) {
    D_ideal_str += " (" + D_ideal[I] + ") ";
  }
  Rest_str = "";
  for (I = 0; I < length(Rest); I++) {
    Rest_str += " (" + rtostr(Rest[I]) + ") ";
  }
  V_str = "";
  for (I = 0; I < length(V); I++) {
    V_str += " (" + rtostr(V[I]) + ") ";
  }
  Cmd_str = "[ [" + D_ideal_str + "] [" + Rest_str + "] [[" + V_str + "] [ ]] 0 ] restriction";
#if _DEBUG_Dmodule_
  print("sm1 command string: ", 0); print(Cmd_str);
#endif
  sm1(dmodule_sm1_start(), Cmd_str);
  return sm1_pop(dmodule_sm1_start());
}

def dmodule_restriction_generic(D_ideal, Param, V, Rest) {
  D_ideal_str = "";
  for (I = 0; I < length(D_ideal); I++) {
    D_ideal_str += " (" + D_ideal[I] + ") ";
  }
  Rest_str = "";
  for (I = 0; I < length(Rest); I++) {
    Rest_str += " (" + rtostr(Rest[I]) + ") ";
  }
  V_str = "";
  for (I = 0; I < length(V); I++) {
    V_str += " (" + rtostr(V[I]) + ") ";
  }
  for (I = 0; I < length(Param); I++) {
    V_str += " (" + rtostr(Param[I]) + ") ";
  }
  Cmd_str = "[ [" + D_ideal_str + "] [" + Rest_str + "] [[" + V_str + "] [ ]] 0 ] restriction";
#if _DEBUG_Dmodule_
  print("sm1 command string: ", 0); print(Cmd_str);
#endif
  sm1(dmodule_sm1_start(), Cmd_str);
  return sm1_pop(dmodule_sm1_start());
}

def dmodule_restriction_appell1(A, B1, B2, C, V, Rest) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell1(A, B1, B2, C, V), V);
  return dmodule_restriction(D_ideal_str, V, Rest);
}

def dmodule_restriction_appell2(A, B1, B2, C1, C2, V, Rest) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell2(A, B1, B2, C1, C2, V), V);
  return dmodule_restriction(D_ideal_str, V, Rest);
}

def dmodule_restriction_appell3(A1, A2, B1, B2, C, V, Rest) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell3(A1, A2, B1, B2, C, V), V);
  return dmodule_restriction(D_ideal_str, V, Rest);
}

def dmodule_restriction_appell4(A, B, C1, C2, V, Rest) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell4(A, B, C1, C2, V), V);
  return dmodule_restriction(D_ideal_str, V, Rest);
}

def dmodule_restriction_generic_appell4(A, B, C1, C2, Param, V, Rest) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell4(A, B, C1, C2, V), V);
  return dmodule_restriction_generic(D_ideal_str, Param, V, Rest);
}

def dmodule_restriction_selberg2(A, B, C, S, V, Rest) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_selberg2(A, B, C, S, V), V);
  return dmodule_restriction(D_ideal_str, V, Rest);
}

def dmodule_restriction_selberg2_1(A, B, K1, K2, V, Rest) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_selberg2_1(A, B, K1, K2, V), V);
  return dmodule_restriction(D_ideal_str, V, Rest);
}

def dmodule_restriction_gkz(A, B, V, Rest) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_gkz(A, B, V), V);
  return dmodule_restriction(D_ideal_str, V, Rest);
}

def dmodule_lookup(X, List) {
  if (type(List) != 4) {
    error("parameter type error.");
  }
  for (I = 0; I < length(List); I++) {
    if (X == List[I]) {
      return 1;
    }
  }
  return 0;
}

/* dmodule_assign0(Rat, [x,y,z]); -> x=0,y=0,z=0 */
def dmodule_assign0(Rat, V) {
  if (type(Rat) > 3 || type(V) != 4) {
    error("parameter type error.");
  }
  for (I = 0; I < length(V); I++) {
    Rat = subst(Rat, V[I], 0);
  }
  return Rat;
}

/* dmodule_assign(Rat, [x,y,z], [1,2]); <-> x=1,y=2,z=0 */
def dmodule_assign(Rat, V, Value) {
  if (type(Rat) > 3 || type(V) != 4 || type(Value) != 4) {
    error("parameter type error.");
  }
  for (I = 0; I < length(V); I++) {
    if (I < length(Value)) {
      Rat = subst(Rat, V[I], Value[I]);
    }
    else {
      Rat = subst(Rat, V[I], 0);
    }
  }
  return Rat;
}

/* dmodule_elimination_order([x,y,z], [y,z])
   -> [ [dy,1,dz,0,dx,0,y,0,z,0,x,0],
        [dy,0,dz,1,dx,0,y,0,z,0,x,0],
        [dy,0,dz,0,dx,1,y,0,z,0,x,0],
        [dy,0,dz,0,dx,0,y,1,z,0,x,0],
        [dy,0,dz,0,dx,0,y,0,z,1,x,0],
        [dy,0,dz,0,dx,0,y,0,z,0,x,1] ] */
def dmodule_elimination_order(V, Elim) {
  if (type(V) != 4 || type(Elim) != 4) {
    error("parameter type error.");
  }
  Dv = dmodule_make_dv(V);
  VarOrderX = Elim;  
  VarOrderD = dmodule_make_dv(Elim);
  for (I = 0; I < length(V); I++) {
    if (!dmodule_lookup(V[I], Elim)) {
      VarOrderX = append(VarOrderX, [V[I]]);
      VarOrderD = append(VarOrderD, [Dv[I]]);
    }
  }
  VarOrder = append(VarOrderD, VarOrderX);
  ElimOrder = [];
  for (I = 0; I < length(VarOrder); I++) {
    Filt = [];
    for (J = 0; J < length(VarOrder); J++) {
      if (J == I) {
	Filt = append(Filt, [VarOrder[J], 1]);
      }
      else {
	Filt = append(Filt, [VarOrder[J], 0]);
      }
    }
    ElimOrder = append(ElimOrder, [Filt]);
  }
#if _DEBUG_Dmodule_
  print("elimination order :", 0); print(ElimOrder);
#endif
  return ElimOrder;
}

/* D_ideal: ["sm1 string format",...] */
/* Elim: eliminated variables and values */
/* Elim: [[x1,...,xt],[a1,...,at]] -> restrict to {x1=a1,...,xt=at} */
/* In the case of D_ideal with no generic parameter. */
def dmodule_elimination(D_ideal, V, Elim) {
  return dmodule_elimination_generic(D_ideal, [], V, Elim);
}

/* In the case of D_ideal with generic parameter. */
def dmodule_elimination_generic(D_ideal, Param, V, Elim) {
#if _DEBUG_Dmodule_
  print("D-idal: ", 0); print(D_ideal);
#endif
  Gb1 = sm1_gb([ D_ideal, dmodule_var_tosm1(append(V, Param)),
	         dmodule_elimination_order(append(V, Param), Elim[0]) ]);
#if _DEBUG_Dmodule_
  print("Gb1: ", 0); print(Gb1);
#endif
  Gb2 = [];
  D_Elim = dmodule_make_dv(Elim[0]);
  for (I = 0; I < length(Gb1[0]); I++) {
    Vars = vars(Gb1[0][I]);
    Flag = 0;
    for (J = 0; J < length(D_Elim); J++) {
      if (dmodule_lookup(D_Elim[J], Vars)) {
	Flag = 1;
	break;
      }
    }
    if (Flag == 0) {
      Gb2 = append(Gb2, [Gb1[0][I]]);
    }
  }
#if _DEBUG_Dmodule_
  print("Gb2: ", 0); print(Gb2);
#endif
  R_Gb = [];
  for (I = 0; I < length(Gb2); I++) {
    if ((Poly = dmodule_assign(Gb2[I], Elim[0], Elim[1])) != 0) {
      R_Gb = append(R_Gb, [Poly]);
    }
  }
#if _DEBUG_Dmodule_
  print("elimination D-ideal: ", 0); print(R_Gb);
#endif
  if (R_GB == []) {
    return [0];
  }
  else {
    return R_Gb;
  }
}

def dmodule_elimination_appell1(A, B1, B2, C, V, Elim) {
  D_idal = dmodule_d_op_tosm1(diff_op_appell1(A, B1, B2, C, V), V);
  return dmodule_elimination(D_ideal, V, Elim);
}

def dmodule_elimination_appell2(A, B1, B2, C1, C2, V, Elim) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell2(A, B1, B2, C1, C2, V), V);
  return dmodule_elimination(D_ideal_str, V, Elim);
}

def dmodule_elimination_appell3(A1, A2, B1, B2, C, V, Elim) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell3(A1, A2, B1, B2, C, V), V);
  return dmodule_elimination(D_ideal_str, V, Elim);
}

def dmodule_elimination_appell4(A, B, C1, C2, V, Elim) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell4(A, B, C1, C2, V), V);
  return dmodule_elimination(D_ideal_str, V, Elim);
}

def dmodule_elimination_appell4s(R_A, R_B, R_C1, R_C2, V, Elim, Flag) {
  Res_list = [];
  for (A = R_A[0]; A <= R_A[1]; A += R_A[2]) {
    for (B = R_B[0]; B <= R_B[1]; B += R_B[2]) {
      for (C1= R_C1[0]; C1 <= R_C1[1]; C1 += R_C1[2]) {
	for (C2= R_C2[0]; C2 <= R_C2[1]; C2 += R_C2[2]) {
	  Res = dmodule_elimination_appell4(A, B, C1, C2, V, Elim);
	  print([[A,B,C1,C2],[Res]]);
	  if (Flag) {
	    Res_list = append(Res_list, [ [[A, B, C1, C2],[Res]] ]);
	  }
	}
      }
    }
  }
  return Res_list;
}

def dmodule_elimination_generic_appell4(A, B, C1, C2, Param, V, Elim) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_appell4(A, B, C1, C2, V), V);
  return dmodule_elimination_generic(D_ideal_str, Param, V, Elim);
}

def dmodule_elimination_selberg2(A, B, C, S, V, Elim) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_selberg2(A, B, C, S, V), V);
  return dmodule_elimination(D_ideal_str, V, Elim);
}

def dmodule_elimination_generic_selberg2(A, B, C, S, Param, V, Elim) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_selberg2(A, B, C, S, V), V);
  return dmodule_elimination_generic(D_ideal_str, Param, V, Elim);
}

def dmodule_elimination_selberg2_1(A, B, K1, K2, V, Elim) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_selberg2_1(A, B, K1, K2, V), V);
  return dmodule_elimination(D_ideal_str, V, Elim);
}

def dmodule_elimination_gkz(A, B, V, Elim) {
  D_ideal_str = dmodule_d_op_tosm1(diff_op_gkz(A, B, V), V);
  return dmodule_elimination(D_ideal_str, V, Elim);
}

/* (V[0] dV[0])^N -> V[0]^N dV[0]^N + ... */
def dmodule_euler2d(N, V) {
  extern Dmodule_Euler2D;
  if (N < 0) {
    error("parameter range error.");
  }
  /* extent a table */
  if (N >= length(Dmodule_Euler2D)) {
    for (I = length(Dmodule_Euler2D); I <= N; I++) {
      List = [0];
      for (J = 1; J < I; J++) {
	List = append(List, [Dmodule_Euler2D[I-1][J-1]+J*Dmodule_Euler2D[I-1][J]]);
      }
      List = append(List, [Dmodule_Euler2D[I-1][I-1]]);
      Dmodule_Euler2D = append(Dmodule_Euler2D, [List]);
    }
  }
  /* calculate by using a table */
  Coef = Dmodule_Euler2D[N];
  Dv = dmodule_make_dv(V);
  D = 0;
  for (I = 0; I < length(Coef); I++) {
    D += Coef[I]*V[0]^I*Dv[0]^I;
  }
  return D;
}

/* Param: List, [a,b,c] */
/* Return: [[a->a+1,a->a-1],[b->b+1,b->b-1],[c->c+1,c->c-1]] */
def dmodule_contiguity_hg1(Param, V) {
  if (length(V) != 1) {
    error("a length of List V is wrong.");
  }
  A = Param[0]; B = Param[1]; C = Param[2];
  return [ [ [[V[0],[1]],[A,[0]]],[[V[0]*(1-V[0]),[1]],[C-A-B*V[0],[0]]] ],
           [ [[V[0],[1]],[B,[0]]],[[V[0]*(1-V[0]),[1]],[C-B-A*V[0],[0]]] ],
           [ [[1-V[0],[1]],[C-A-B,[0]]],[[V[0],[1]],[C-1,[0]]] ] ];
}

def dmodule_contiguity_appell4(Param, V) {
  if (length(V) != 2) {
    error("a length of List V is wrong.");
  }
  A = Param[0]; B = Param[1]; C1 = Param[2]; C2 = Param[3];
  if (A == 0 || A == C1 || A == C2 || A == C1+C2-1) {
    print("contiguity of "+rtostr(A)+" is not isomorphism.");
  }
  if (B == 0 || B == C1 || B == C2 || B == C1+C2-1) {
    print("contiguity of "+rtostr(B)+" is not isomorphism.");
  }
  if (A == C1 || B == C1 || A == C1+C2-1 || B == C1+C2-1) {
    print("contiguity of "+rtostr(C1)+" is not isomorphism.");
  }
  if (A == C1 || B == C1 || A == C1+C2-1 || B == C1+C2-1) {
    print("contiguity of "+rtostr(C2)+" is not isomorphism.");
  }
  return [ [ [[V[0],[1,0]],[V[1],[0,1]],[A,[0,0]]],
             [[(4*V[1]*V[0]^2),[2,0]],[(-8*(A-1)+4*C1+4*C2-8)*V[1]*V[0]^2+((8*(A-1)-4*C1-4*C2+16)*V[1]^2+(8*(A-1)-4*C1-4*C2+8)*V[1])*V[0],[1,1]],[(-8*(A-1)+4*C1+4*C2-8)*V[1]^2*V[0]+(8*(A-1)-4*C1-4*C2+12)*V[1]^3+(-8*(A-1)+4*C1+4*C2-12)*V[1]^2,[0,2]],[(4*(A-1)^2+(-4*C1-8*C2+12)*(A-1)+(4*C2-4)*C1+4*C2^2-12*C2+8)*V[0]^2+((12*(A-1)^2+(-12*C1-8*C2+32)*(A-1)+4*B+4*C1^2+(4*C2-16)*C1-8*C2+24)*V[1]-4*(A-1)^2+(4*C1+8*C2-12)*(A-1)+(-4*C2+4)*C1-4*C2^2+12*C2-8)*V[0],[1,0]],[(4*(A-1)^2+(-8*B-4*C1-8*C2+4)*(A-1)+(4*C1+4*C2-8)*B+4*C2*C1+4*C2^2-8*C2)*V[1]*V[0]+(12*(A-1)^2+(8*B-12*C1-8*C2+40)*(A-1)+(-4*C1-4*C2+12)*B+4*C1^2+(4*C2-20)*C1-12*C2+32)*V[1]^2+(-4*(A-1)^2+(8*C1-4*C2-12)*(A-1)-4*C1^2+12*C1+4*C2^2-8*C2-8)*V[1],[0,1]],[(4*B*(A-1)^2+(-4*C1-8*C2+12)*B*(A-1)+((4*C2-4)*C1+4*C2^2-12*C2+8)*B)*V[0]+(12*B*(A-1)^2+(-12*C1-8*C2+32)*B*(A-1)+(4*C1^2+(4*C2-16)*C1-8*C2+20)*B)*V[1]+4*(A-1)^3+(-8*C1-8*C2+16)*(A-1)^2+(4*C1^2+(12*C2-20)*C1+4*C2^2-20*C2+20)*(A-1)+(-4*C2+4)*C1^2+(-4*C2^2+16*C2-12)*C1+4*C2^2-12*C2+8,[0,0]]] ],
           [ [[V[0],[1,0]],[V[1],[0,1]],[B,[0,0]]],
             [[4*V[1]*V[0]^2,[2,0]],[(-8*(B-1)+4*C1+4*C2-8)*V[1]*V[0]^2+((8*(B-1)-4*C1-4*C2+16)*V[1]^2+(8*(B-1)-4*C1-4*C2+8)*V[1])*V[0],[1,1]],[(4*(B-1)^2+(-4*C1-8*C2+12)*(B-1)+(4*C2-4)*C1+4*C2^2-12*C2+8)*V[0]^2+((4*A+12*(B-1)^2+(-12*C1-8*C2+32)*(B-1)+4*C1^2+(4*C2-16)*C1-8*C2+24)*V[1]-4*(B-1)^2+(4*C1+8*C2-12)*(B-1)+(-4*C2+4)*C1-4*C2^2+12*C2-8)*V[0],[1,0]],[(-8*(B-1)+4*C1+4*C2-8)*V[1]^2*V[0]+(8*(B-1)-4*C1-4*C2+12)*V[1]^3+(-8*(B-1)+4*C1+4*C2-12)*V[1]^2,[0,2]],[((-8*(B-1)+4*C1+4*C2-8)*A+4*(B-1)^2+(-4*C1-8*C2+4)*(B-1)+4*C2*C1+4*C2^2-8*C2)*V[1]*V[0]+((8*(B-1)-4*C1-4*C2+12)*A+12*(B-1)^2+(-12*C1-8*C2+40)*(B-1)+4*C1^2+(4*C2-20)*C1-12*C2+32)*V[1]^2+(-4*(B-1)^2+(8*C1-4*C2-12)*(B-1)-4*C1^2+12*C1+4*C2^2-8*C2-8)*V[1],[0,1]],[(4*(B-1)^2+(-4*C1-8*C2+12)*(B-1)+(4*C2-4)*C1+4*C2^2-12*C2+8)*A*V[0]+(12*(B-1)^2+(-12*C1-8*C2+32)*(B-1)+4*C1^2+(4*C2-16)*C1-8*C2+20)*A*V[1]+4*(B-1)^3+(-8*C1-8*C2+16)*(B-1)^2+(4*C1^2+(12*C2-20)*C1+4*C2^2-20*C2+20)*(B-1)+(-4*C2+4)*C1^2+(-4*C2^2+16*C2-12)*C1+4*C2^2-12*C2+8,[0,0]]] ],
           [ [[(-A-B+2*(C1+1)+C2-3)*V[1]*V[0]+(-A-B+2*(C1+1)+C2-3)*V[1]^2+(A+B-2*(C1+1)-C2+3)*V[1],[1,1]],[((B-(C1+1)-C2+2)*A+(-(C1+1)-C2+2)*B+(C1+1)^2+(2*C2-4)*(C1+1)+C2^2-4*C2+4)*V[0]+(-A^2+(-B+3*(C1+1)+C2-5)*A-B^2+(3*(C1+1)+C2-5)*B-3*(C1+1)^2+(-2*C2+10)*(C1+1)+3*C2-8)*V[1]+(-B+(C1+1)+C2-2)*A+((C1+1)+C2-2)*B-(C1+1)^2+(-2*C2+4)*(C1+1)-C2^2+4*C2-4,[1,0]],[(-2*A-2*B+4*(C1+1)+2*C2-6)*V[1]^2,[0,2]],[(-A^2+((C1+1)-C2-2)*A-B^2+((C1+1)-C2-2)*B+(3*C2+1)*(C1+1)+2*C2^2-5*C2-1)*V[1],[0,1]],[(B-(C1+1)-C2+2)*A^2+(B^2+(-3*(C1+1)-2*C2+5)*B+2*(C1+1)^2+(3*C2-7)*(C1+1)+C2^2-5*C2+6)*A+(-(C1+1)-C2+2)*B^2+(2*(C1+1)^2+(3*C2-7)*(C1+1)+C2^2-5*C2+6)*B-(C1+1)^3+(-2*C2+5)*(C1+1)^2+(-C2^2+6*C2-8)*(C1+1)+C2^2-4*C2+4,[0,0]]],
             [[V[0],[1,0]],[C1-1,[0,0]]] ],
           [ [[(-2*A-2*B+2*C1+4*(C2+1)-6)*V[0]^2,[2,0]],[(-A-B+C1+2*(C2+1)-3)*V[0]^2+((-A-B+C1+2*(C2+1)-3)*V[1]+A+B-C1-2*(C2+1)+3)*V[0],[1,1]],[(-A^2+(-C1+(C2+1)-2)*A-B^2+(-C1+(C2+1)-2)*B+2*C1^2+(3*(C2+1)-5)*C1+(C2+1)-1)*V[0],[1,0]],[(-A^2+(-B+C1+3*(C2+1)-5)*A-B^2+(C1+3*(C2+1)-5)*B+(-2*(C2+1)+3)*C1-3*(C2+1)^2+10*(C2+1)-8)*V[0]+((B-C1-(C2+1)+2)*A+(-C1-(C2+1)+2)*B+C1^2+(2*(C2+1)-4)*C1+(C2+1)^2-4*(C2+1)+4)*V[1]+(-B+C1+(C2+1)-2)*A+(C1+(C2+1)-2)*B-C1^2+(-2*(C2+1)+4)*C1-(C2+1)^2+4*(C2+1)-4,[0,1]],[(B-C1-(C2+1)+2)*A^2+(B^2+(-2*C1-3*(C2+1)+5)*B+C1^2+(3*(C2+1)-5)*C1+2*(C2+1)^2-7*(C2+1)+6)*A+(-C1-(C2+1)+2)*B^2+(C1^2+(3*(C2+1)-5)*C1+2*(C2+1)^2-7*(C2+1)+6)*B+(-(C2+1)+1)*C1^2+(-2*(C2+1)^2+6*(C2+1)-4)*C1-(C2+1)^3+5*(C2+1)^2-8*(C2+1)+4,[0,0]]],
             [[V[1],[0,1]],[C2-1,[0,0]]] ] ];
}

/* Sol(a,b,c1,c2)->[ [Sol(a+1,b,c1,c2),Sol(a-1,b,c1,c2)],
                     [Sol(a,b+1,c1,c2),Sol(a,b-1,c1,c2)],
                     [Sol(a,b,c1+1,c2),Sol(a,b,c1-1,c2)],
                     [Sol(a,b,c1,c2+1),Sol(a,b,c1,c2-1)] ] */
def dmodule_contiguity_sol_appell4(Sol, Param, V) {
  C = dmodule_contiguity_appell4(Param, V);
  C_Sol = [diff_acts(C[0], Sol, V),
           diff_acts(C[1], Sol, V),
           diff_acts(C[2], Sol, V),
           diff_acts(C[3], Sol, V)];
  return C_Sol;
}

def dmodule_contiguity_selberg2(Param, V) {
  if (length(V) != 2) {
    error("a length of List V is wrong.");
  }
  A = Param[0]; B = Param[1]; C = Param[2]; S = Param[3];
}

end$
