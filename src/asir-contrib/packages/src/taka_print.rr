/* $OpenXM: OpenXM/src/asir-contrib/packages/src/taka_print.rr,v 1.5 2001/08/21 06:25:59 takayama Exp $ */

#define Number 1
#define RPolynomial 2
#define Rational 3
#define List 4
#define Vector 5
#define Matrix 6
#define STRING 7
#define STRUCT 8
#define DPolynomial   9
#define QUOTE 17

Taka_print_dp_vlist_vname="x"$
def taka_print_dp_vlist(N) {
  extern Taka_print_dp_vlist_vname;
  V = Taka_print_dp_vlist_vname+"_"+rtostr(N);
  return strtov(V);
}
  /* Used in taka_tex_form_dpolynomial
     and taka_dp_print  */

def taka_tex_form(S) {
  if (type(S) == Matrix) {
     return taka_tex_form_matrix(S);
  }else if (type(S) == Vector) {
     return taka_tex_form_vector(S);
  }else if (type(S) == List) {
     return taka_tex_form_list(S);
  }else if (taka_is_fractional(S)) {
     return( taka_tex_form_frac(S) );
  }else if (type(S) == DPolynomial) {
     return taka_tex_form_dpolynomial(S);
  }else if (type(S) == RPolynomial) {
     return taka_tex_form_rpolynomial(S);
  }else if (type(S) == STRUCT) {
     return taka_tex_form_struct(S);
  }else if (type(S) == QUOTE) {
     return taka_tex_form_quote(S);
  }else {
   /* How to translate sin to \sin?
      Use vtype, get args, ... to print sin. 
      quote("x+2*y");  there is no way to get the left leaves and right leaves
      for now.   eval_quote.
   */
     return rtostr(S);
  }
}

def taka_tex_form_matrix(A) {
  N = size(A)[0];
  M = size(A)[1];
  S="\\pmatrix{\n";
  for (I=0; I<N; I++) {
    for (J=0; J<M; J++) {
      S += taka_tex_form(A[I][J]);
      if (J != M-1) S += "& ";
    }
    S += " \\cr\n";
  }
  S += "}\n";
  return S;
}

def taka_tex_form_vector(A) {
  N = size(A)[0];
  S="\\pmatrix{\n";
  for (I=0; I<N; I++) {
    S += taka_tex_form(A[I]);
    if (I != N-1) S += "& ";
  }
  S += "\\cr}\n";
  return S;
}

def taka_tex_form_list(A) {
  N = length(A);
  S="[";
  for (I=0; I<N; I++) {
    S += taka_tex_form(A[I]);
    if (I != N-1) S += ", ";
  }
  S += "]";
  return S;
}

def taka_is_fractional(A) {
  if (type(A) == Rational) return 1;
  else if (type(A) == Number && dn(A) != 1) return 1;
  else return 0;
}

def taka_tex_form_frac(A) {
  S = "\\frac{";
  S += taka_tex_form(nm(A))+"}{";
  S += taka_tex_form(dn(A))+"}";
  return S;
}

def taka_tex_form_dpolynomial(F) {
  if (F != 0) {
    N = size(dp_etov(dp_hm(F)))[0];
    Vlist = newvect(N);
    for (I=0; I<N; I++) {
      Vlist[I] = taka_print_dp_vlist(I);
    }
    Vlist = vtol(Vlist);
    Ans="";
  }else{
    Ans="0";
  }
  C = 0;
  while (F != 0) {
     G = dp_hm(F);
     F = dp_rest(F);
     if (dp_hc(G)>0 && C != 0) {
       Ans += "+";
     }
     Ans += taka_tex_form(dp_dtop(G,Vlist));
     if (C == 0) C = 1;
  }
  return Ans;
}

def taka_tex_form_rpolynomial(S) {
  return taka_tex_form_quote(quote_to_quote(S));
}

/* Tentative code */
def taka_tex_form_struct(S) {
  if (S->Tag == "poly_factored_polynomial" || S->Tag == "poly_factored_rational") {
     return taka_tex_form_quote( quote_factored_form_to_quote(S));
  }else {
     print("Unknown struct object for tex_form. Return ???");
     return "???";
  }
}

def taka_tex_form_quote(S) {
  S = quotetolist(S);
  return taka_tex_form_quote_list(S);
}

def taka_tex_form_quote_list(S) {
  if (type(S) != List) return taka_tex_form_node_name(S);
  A = " ";
  if (length(S) < 2) return taka_tex_form_node_name(S[0]);
  if (S[0] == "+") {
    T = S[1];
    N = length(T);
    for (I=0; I<N; I++) {
      A += taka_tex_form_quote_list(T[I]);
        if (I != N-1) A += S[0];
    }
  }else
  if (S[0] == "-") {
    T = S[1];
    N = length(T);
    for (I=0; I<N; I++) {
      if (I == 0 && T[I] == 0) {
         /* Do not print 0. */
      } else {
         A += taka_tex_form_quote_list(T[I]);
      }
      if (I != N-1) A += S[0];
    }
  } else
  if (S[0] == "*") {
    T = S[1];
    N = length(T);
    for (I=0; I<N; I++) {
      A += taka_tex_form_quote_list(T[I]);
      A += " ";
    }
  } else
  if (S[0] == "/") {
    T = S[1];
    A += "\\frac{";
    A += taka_tex_form_quote_list(T[0]);
    A += "} {";
    A += taka_tex_form_quote_list(T[1]);
    A += "} ";
  } else
  if (S[0] == "^") {
    T = S[1];
    A += taka_tex_form_quote_list(T[0]);
    A += "^{";
    A += taka_tex_form_quote_list(T[1]);
    A += "} ";
  } else
  if (S[0] == "()") {
    A += "(";
    A += taka_tex_form_quote_list(S[1]);
    A += ") ";
  }else
  {   
    /* Node is assumed to be a function name */
    A += taka_tex_form_node_name(S[0]);
    A += "(";
    T = S[1];
    N = length(T);
    for (I=0; I<N; I++) {
      A += taka_tex_form_quote_list(T[I]);
      if (I != N-1) {
        A += ", ";
      }
    }
    A += ")";
  }
  return(A);
}

def taka_tex_form_node_name(S) {
  if (type(S) != STRING) return rtostr(S);
  /* Table of Mathematical Symbols. */
  if (S == "sin") {
     return "\\sin ";
  }else if (S == "cos") {
     return "\\cos ";
  }else if (S == "tan") {
     return "\\tan ";
  }else if (S == "exp") {
     return "\\exp ";
  }else if (S == "log") {
     return "\\log ";
  }else if (S == "alpha") {
     return "\\alpha";
  }else if (S == "beta") {
     return "\beta";
  }else if (S == "gamma") {
     return "\\gamma";
  }else if (S == "ggamma") {
     return "\\Gamma";
  }else {
     return S;
  }
}

def taka_output(S) {
  shell("rm t.tex ");
  output("t.tex");
  print(S);
  output();
}

def taka_print(S) {
  if (type(S) == DPolynomial) {
    return taka_dp_print(S);
  }else{
    return rtostr(S);
  }
}
def taka_dp_print(F) {
  if (F != 0) {
    N = size(dp_etov(dp_hm(F)))[0];
    Vlist = newvect(N);
    for (I=0; I<N; I++) {
      Vlist[I] = taka_print_dp_vlist(I);
    }
    Vlist = vtol(Vlist);
    Ans="distributed_polynomial(\"   ";
  }else{
    Ans="0";
  }
  C = 0;
  while (F != 0) {
     G = dp_hm(F);
     F = dp_rest(F);
     if (dp_hc(G)>0 && C != 0) {
       Ans += "+";
     }
     Ans += rtostr(dp_dtop(G,Vlist));
     if (C == 0) C = 1;
  }
  Ans += "   \")";
  return Ans;
}

def taka_xdvi_form(S) {
  S = taka_tex_form(S);
  File0 = "/tmp/asir_"+"print_tmp";
  File = File0+".tex";
  shell("rm -f "+File);
  output(File);
  S = "\\documentclass[12pt]{article} \\pagestyle{empty} \\begin{document}\n"+
      "$"+
      S + "$ \n \\end{document}\n";
  print(S);
  output();
  shell(" (cd /tmp ; latex "+File0+" >>/dev/null) ");
  /* Use xdvi for a moment. */
  shell(" (cd /tmp ; xdvi -s 3 -geometry 800x300 "+File0+" ) >>/dev/null & ");
  return [File,File0+".dvi"];
}


Loaded_taka_print=1$
end$
